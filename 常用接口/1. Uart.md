# 三大低速协议
Uart、I2C、SPI

<img src=fig1\三大低速协议.gif style="zoom:50%">

# 基本概念

Uart（Universal Asynchronous Receiver/Transmitter）通用异步（通信时没有时钟）收发传输器，是一种异步收发传输器，是设备间进行异步通信的关键模块

Uart负责数据总线和串行口之间的串/并、并/串转换，并规定了帧格式；**通信双方只要采用相同的帧格式**，就能在未共享时钟信号的情况下，**仅用两根信号线（RX和TX）** 就可以完成通信过程，因此也称为异步串行通信

Uart可以实现**全双工**传输和接收

Uart协议广泛用于微控制机、计算机和其它数字设备之间的通信

# 数据包格式

Uart中传输模式为数据包形式，数据包由起始位、数据帧、奇偶校验位和停止位组成

<img src=fig1\Uart数据帧格式.png style="zoom: 70%">

数据通信形式如下：

<img src=fig1\Uart通讯格式.png style="zoom: 70%">

Uart一般先发LSB，再发MSB

**比特率**：$R_{b}$，每秒传输的比特数，$bit/s$

**波特率**：$R_{s}$，每秒传输的符号数，$baud$

二者的关系：$R_{b} = R_{s} * log_{2}N$

在串口传输时，符号为二进制，因此波特率和比特率在数值上相等；串口常见的波特率为：9600，115200等，以115200的波特率为例，其每个比特的持续时间为：$1s/115200 = 8680.6ns$

**1、起始位**：Uart数据传输线在空闲状态时保持高电平，当主机先发出一个逻辑“0”的信号，表示传输数据的开始

**2、数据位**：要发送的数据，一般位宽为8

**3、校验区**：在串口通信中一种简单的检错方式（四种检错方式：奇校验，偶校验，高校验，低校验）。当然没有校验位也是可以的，对于奇偶校验的情况，串口会设置校验位（数据位的最后一位），用1bit确保传输的数据有奇数个或偶数个逻辑高位；一般选用奇偶校验

**4、停止区**：一个字符数据的结束标志，可以是1位、1.5位、2位的高电平

# 电平标准
Uart分为RS-232、RS-422、RS-485


| 特性         | RS-232                           | RS-422                 | RS-485                         |
| ------------ | -------------------------------- | ---------------------- | ------------------------------ |
| 信号传输方式 | 单端信号（与地相对）             | 差分信号               | 差分信号                       |
| 最大通信距离 | 15米（50英尺）                   | 1200米（4000英尺）     | 1200米（4000英尺）             |
| 最大传输速率 | 115200 bps                       | 10 Mbps                | 10 Mbps                        |
| 传输方式     | 全双工                           | 全双工                 | 半全双工                       |
| 适用设备数量 | 点对点通信（1对1）               | 点对多接收器（1对多）  | 多点通信（最多32个设备）       |
| 抗干扰性     | 较差                             | 较好                   | 极好                           |
| 常见应用     | 计算机与外设、调制解调器、打印机 | 工业控制、远程数据采集 | 工业自动化、现场总线、智能仪表 |

开发板一般是USB转串口，三者皆不属于

# 普通串口代码实现
## 发送端
```verilog
`timescale 1ns / 1ps

module uart_tx #(
	parameter baud	= 115200,
	parameter check	= 0					// 0：无校验		1：奇校验     2：偶校验 
)
(
	input			clk				,
	input			rst_n			,
		
	input			tx_de			,	// 待发送的8位并行数据使能
	input [7:0]		tx_data			,	
		
	output reg		tx				,	// 将8位并行数据转为串行输出数据帧
	output reg		tx_done				// 表明当前并行数据已发出，可以接收下一个字节
);
	reg [7:0]		tx_data_latch	;
	reg				tx_flag			;
	reg [15:0]		baud_cnt		;
	reg [7:0]		baud_time		;
	reg				bit_flag		;
	reg [7:0]		bit_cnt			;
	
	wire			check_sum		;
	wire [7:0]		bit_num			;
	wire [15:0]		baud_end		;
	wire [15:0]		baud_mid		;
	
	// baud_end是单个比特持续周期数
	// 以波特率为115200为例，1s = 1000000000ns
	// 一个比特持续时间为：1000000000 / 115200 = 8680ns
	// 在100MHz的时钟下，一个时钟周期为10ns
	// 因此一个比特持续868个周期
	// 这就是baud end的由来
	generate
		if(baud == 115200) begin: _115200
			assign baud_end = 868;		
			assign baud_mid = 434;
		end
		else if(baud == 230400) begin: _230400
			assign baud_end = 434;
			assign baud_mid = 217;
		end
		else if(baud == 460800) begin: _460800
			assign baud_end = 217;
			assign baud_mid = 108;
		end
		else if(baud == 921600) begin: _921600
			assign baud_end = 108;
			assign baud_mid = 54;
		end
	endgenerate
	
	generate
		if(check == 0)
			begin: none			// 不校验，一个数据帧的长度为：起始位1 + 数据位8 + 停止位1 = 总位10
				assign check_sum 	= 0;
				assign bit_num		= 10;
				always @(posedge clk) begin
					if(!rst_n)
						tx	<= 1'b1;
					else if(bit_flag == 1'b1)
						case(bit_cnt)
							8'd0:		tx <= 1'b0;
							8'd1:		tx <= tx_data_latch[0];
							8'd2:		tx <= tx_data_latch[1];
							8'd3:		tx <= tx_data_latch[2];
							8'd4:		tx <= tx_data_latch[3];
							8'd5:		tx <= tx_data_latch[4];
							8'd6:		tx <= tx_data_latch[5];
							8'd7:		tx <= tx_data_latch[6];
							8'd8: 		tx <= tx_data_latch[7];
							8'd9:		tx <= 1'b1;
							default:	tx <= 1'b1;
						endcase
				end
			end
		else if(check == 1)
			begin: odd
				assign bit_num		= 11;
				assign check_sum	= ~(^tx_data_latch);
				always @(posedge clk) begin
					if(!rst_n)
						tx			<= 1'b1;
					else if(bit_flag == 1'b1)
						case(bit_cnt)
							8'd0:		tx <= 1'b0;
							8'd1:		tx <= tx_data_latch[0];
							8'd2:		tx <= tx_data_latch[1];
							8'd3:		tx <= tx_data_latch[2];
							8'd4:		tx <= tx_data_latch[3];
							8'd5:		tx <= tx_data_latch[4];
							8'd6:		tx <= tx_data_latch[5];
							8'd7:		tx <= tx_data_latch[6];
							8'd8:		tx <= tx_data_latch[7];
							8'd9:		tx <= check_sum;
							8'd10:		tx <= 1'b1;
							default:	tx <= 1'b1;
						endcase
				end
			end
		else
			begin: even
				assign bit_num		= 11;
				assign check_sum	= ^(tx_data_latch);
				always @(posedge clk) begin
					if(!rst_n)
						tx			<= 1'b1;
					else if(bit_flag == 1'b1)
						case(bit_cnt)
							8'd0:		tx <= 1'b0;
							8'd1:		tx <= tx_data_latch[0];
							8'd2:		tx <= tx_data_latch[1];
							8'd3:		tx <= tx_data_latch[2];
							8'd4:		tx <= tx_data_latch[3];
							8'd5:		tx <= tx_data_latch[4];
							8'd6:		tx <= tx_data_latch[5];
							8'd7:		tx <= tx_data_latch[6];
							8'd8:		tx <= tx_data_latch[7];
							8'd9:		tx <= check_sum;
							8'd10:		tx <= 1'b1;
							default:	tx <= 1'b1;
						endcase
				end
			end
	endgenerate
	
	// 当数据有效时，将数据所存
	// 因为tx_data是并行数据，只能存留一个周期
	always @(posedge clk) begin
		if(!rst_n)
			tx_data_latch	<= 0;
		else if(tx_de == 1'b1)
			tx_data_latch	<= tx_data;
		else
			tx_data_latch	<= tx_data_latch;
	end
			
	// tx_flag在起始位开始的那个时刻拉高，在停止位结束的那个时刻拉低
	always @(posedge clk) begin
		if(!rst_n)
			tx_flag			<= 1'b0;
		else if(baud_cnt == baud_end - 1 && baud_time == bit_num - 1)
			tx_flag			<= 1'b0;
		else if(tx_de == 1'b1)
			tx_flag			<= 1'b1;
		else
			tx_flag			<= tx_flag;
	end
	
	// baud_cnt用于计算单个比特持续了多少个时钟周期
	always @(posedge clk) begin
		if(!rst_n)
			baud_cnt		<= 16'd0;
		else if(baud_cnt == baud_end - 1)
			baud_cnt		<= 16'd0;
		else if(tx_flag == 1'b1)
			baud_cnt		<= baud_cnt + 1'b1;
		else
			baud_cnt 		<= baud_cnt;
	end
	
	// baud_time用于记录当前是数据帧的第几个比特
	always @(posedge clk) begin
		if(!rst_n)
			baud_time		<= 8'd0;
		else if(baud_cnt == baud_end - 1 && baud_time == bit_num - 1)
			baud_time		<= 8'd0;
		else if(baud_cnt == baud_end - 1)
			baud_time		<= baud_time + 1'b1;
		else
			baud_time		<= baud_time;
	end
	
	// 当处于一个比特持续周期的中间时刻时，bit_flag拉高
	always @(posedge clk) begin
		if(!rst_n)
			bit_flag		<= 1'b0;
		else if(baud_cnt == baud_mid - 1)
			bit_flag		<= 1'b1;
		else
			bit_flag 		<= 1'b0;
	end
	
	always @(posedge clk) begin
		if(!rst_n)
			bit_cnt			<= 8'd0;
		else if(bit_flag == 1'b1 && bit_cnt == bit_num - 1)
			bit_cnt			<= 8'd0;
		else if(bit_flag == 1'b1)
			bit_cnt			<= bit_cnt + 1'b1;
		else
			bit_cnt			<= bit_cnt;
	end
	
	
	// tx_done再发送完成后拉高一个周期
	always @(posedge clk) begin
		if(!rst_n)
			tx_done			<= 1'b0;
		else if(baud_cnt == baud_end - 1 && baud_time == bit_num - 1)
			tx_done			<= 1'b1;
		else
			tx_done			<= 1'b0;
	end
	
endmodule
```

## 接收端
```verilog
`timescale 1ns / 1ps

module uart_rx #(
	parameter baud		= 115200	,
	parameter check		= 0	
)	
(	
	input 				clk			,
	input				rst_n		,
		
	input				rx			,
		
	output reg			rx_de		,
	output reg [7:0]	rx_data				// 将接收到的串行数据转为并行数据
);	
	reg					rx_r1		;
	reg					rx_r2		;
	reg					rx_r3		;
		
	wire				rx_nege		;
		
	reg					rx_flag		;
		
	wire [7:0]			bit_num		;
	wire 				check_sum	;
	
	wire [15:0]			baud_end	;
	wire [15:0]			baud_mid	;
	
	reg [15:0]			baud_cnt	;
	reg [7:0]			baud_time	;
	
	reg					bit_flag	;
	reg [7:0]			bit_cnt		;
	
	reg					recv_de		;
	reg [7:0]			recv_data	;
	
	generate
		if(check == 0)
			begin: none
				assign bit_num 		= 10;
				assign check_sum	= 0;
			end
		else if(check == 1)
			begin: odd
				assign bit_num		= 11;
				assign check_sum	= ~(^recv_data);
			end
		else
			begin: even
				assign bit_num		= 11;
				assign check_sum	= (^recv_data);
			end
	endgenerate
	
	generate
		if(baud == 115200)
			begin: _115200
				assign baud_end 	= 868;
				assign baud_mid		= 434;
			end
		else if(baud == 230400)
			begin: _230400
				assign baud_end 	= 434;
				assign baud_mid		= 217;
			end
		else if(baud == 460800)
			begin: _460800
				assign baud_end		= 217;
				assign baud_mid		= 108;
			end
		else if(baud == 921600)
			begin: _921600
				assign baud_end		= 108;
				assign baud_mid		= 54;
			end
	endgenerate			

	// Uart本质是异步通信，数据帧是完全异步于内部时钟的，因此要跨时钟域处理
	always @(posedge clk) begin
		if(!rst_n)
			begin
				rx_r1 <= 1'b1;
				rx_r2 <= 1'b1;
				rx_r3 <= 1'b1;
			end
		else
			begin
				// 跨时钟域，先给信号打两拍
				rx_r1 <= rx;
				rx_r2 <= rx_r1;
				rx_r3 <= rx_r2;
			end
	end
	
	// 下降沿，意味着串口要开始发送数据了
	assign rx_nege = ~rx_r2 && rx_r3;
	
	// 这样安排分支的好处就是检测到起始位的下降沿rx_flag就拉高
	// 只有传输完一个数据帧后，rx_flag才拉低
	always @(posedge clk) begin
		if(!rst_n)
			rx_flag	<= 1'b0;
		else if(baud_cnt == baud_end - 1 && baud_time == bit_num - 1)
			rx_flag	<= 1'b0;
		else if(rx_nege == 1'b1)
			rx_flag <= 1'b1;
		else
			rx_flag <= rx_flag;
	end
			
	always @(posedge clk) begin
		if(!rst_n)
			baud_cnt <= 0;
		else if(baud_cnt == baud_end - 1)
			baud_cnt <= 0;
		else if(rx_flag == 1'b1)
			baud_cnt <= baud_cnt + 1'b1;
		else
			baud_cnt <= baud_cnt;
	end
	
	always @(posedge clk) begin
		if(!rst_n)
			baud_time <= 0;
		else if(baud_cnt == baud_end - 1 && baud_time == bit_num - 1)
			baud_time <= 0;
		else if(baud_cnt == baud_end - 1)
			baud_time <= baud_time + 1'b1;
		else
			baud_time <= baud_time;
	end
	
	always @(posedge clk) begin
		if(!rst_n)
			bit_flag <= 1'b0;
		else if(baud_cnt == baud_mid - 1)
			bit_flag <= 1'b1;
		else
			bit_flag <= 1'b0;
	end
	
	always @(posedge clk) begin
		if(!rst_n)
			bit_cnt <= 0;
		else if(bit_flag == 1'b1 && bit_cnt == bit_num - 1)
			bit_cnt <= 0;
		else if(bit_flag == 1'b1)
			bit_cnt <= bit_cnt + 1'b1;
		else
			bit_cnt <= bit_cnt;
	end
	
	always @(posedge clk) begin
		if(!rst_n)
			recv_de <= 1'b0;
		else if(bit_flag == 1'b1 && bit_cnt == 8)
			recv_de <= 1'b1;
		else
			recv_de <= 1'b0;
	end
	
	always @(posedge clk) begin
		if(!rst_n)
			recv_data <= 1'b0;
		else if(bit_flag == 1'b1 && bit_cnt >= 1 && bit_cnt <= 8)
			recv_data <= {rx, recv_data[7:1]};
		else
			recv_data <= recv_data;
	end
	
	always @(posedge clk) begin
		if(!rst_n)
			rx_de <= 1'b0;
		else if(recv_de == 1'b1 && check == 0)
			// 如果没有校验，直接令接收信号有效
			rx_de <= 1'b1;
		else if(bit_flag == 1'b1 && bit_cnt == bit_num - 2 && check_sum == rx && check != 0)
			// 如果有校验，则要检验接收到的校验位是否与计算出来的校验位相等
			// 相等才能接收
			rx_de <= 1'b1;
		else
			rx_de <= 1'b0;
	end
	
	always @(posedge clk) begin
		if(!rst_n)
			rx_data <= 0;
		else if(recv_de == 1'b1 && check == 0)
			rx_data <= recv_data;
		else if(bit_flag == 1'b1 && bit_cnt == bit_num - 2 && check_sum == rx && check != 0)
			rx_data <= recv_data; 
		else
			rx_data <= rx_data;
	end
	
endmodule
```

# 带FIFO串口代码实现
在实际应用中，一般是一次性发送一个数据包（包内有多个字节），进入空闲状态，再发一个或多个数据包，再进入循环状态，依此类推。解的时候也是一包一包的解。因此需要引入FIFO作为缓存；日常使用的FIFO一般都是带FIFO的

带FIFO的串口就是在上面代码的基础上，增加了一个带FIFO缓冲区的Uart控制器，发送端控制器代码实现如下：

```verilog
module uart_tx_buf (
	input 				clk	 		,	
	input 				rst_n	 	,
	
	input 				rx_de		,
    input 		[7:0] 	rx_data		,
	
	input				tx_done		,
	
	output reg  		tx_de		,
	output 		[7:0]	tx_data			
);				
	wire 		empty	;

	reg  [1:0]  state   ;

	fifo_uart_tx_buf u1_fifo_uart_tx_buf (
		.wr_clk			(clk		), 
		.rd_clk			(clk		), 
		.din			(rx_data	), 
		.wr_en			(rx_de		), 
		.rd_en			(tx_de		), 
		.dout			(tx_data	), 
		.full			(			), 
		.empty			(empty		)
	);	
				
	always@(posedge clk)
		if(!rst_n)
			begin
				tx_de <= 1'b0;
				state <= 0;
			end
		else begin
			case(state)
				0:   
					if(empty==1'b0)
						begin
							tx_de <= 1'b0;
							state <= 1;
						end	
					else
						begin
							tx_de <= 1'b0;
							state <= state;
						end
				1 :   
					begin 
						tx_de <= 1'b1;
						state <= 2;
					end
				2 :   
					if(empty==1'b1)
						begin
							tx_de <= 1'b0;
							state <= 0;
						end
					else if(tx_done==1'b1&&empty==1'b0)
						begin
							tx_de <= 1'b1;
							state <= state;
						end
					else
						begin
							tx_de <= 1'b0;
							state <= state;
						end
				default:	
						begin
							tx_de <= 1'b0;
							state <= 0;
						end	   
			endcase
		end

endmodule
```

其核心思想可以用下面状态机表示：

<img src = fig1\流程图.png>

数据源将要发送的多个数据写入fifo，uart串口再将fifo中的数据送出，需要注意的是fifo中数据写入的速度远大于fifo中数据读出的速度，在第一个比特还没被Uart发出时，数据可能就全部写入到FIFO中了

接收端控制器知道一次发送的数据包中的数据个数，其代码实现如下：

```verilog
module uart_rx_buf #(
	parameter data_len = 32
)
(
	input 				clk	 		,	
	input 				rst_n	 	,
	
	input 				rx_de		,
    input 		[7:0] 	rx_data		,
	
	output reg  		tx_de		,
	output 		[7:0]	tx_data			
);
	
    wire			empty			;	
    wire 	[10:0]  rd_data_count	;

    reg		[7:0]   tx_cnt			;

    fifo_uart_rx_buf u1_fifo_uart_rx_buf (
        .wr_clk			(clk			), 
        .rd_clk			(clk			), 
        .din			(rx_data		), 
        .wr_en			(rx_de			), 
        .rd_en			(tx_de			), 
        .dout			(tx_data		), 
        .full			(				), 
        .empty			(empty			),
        .rd_data_count	(rd_data_count	),
        .wr_data_count	(				)
    );	
	always@(posedge clk)
		if(!rst_n)
			tx_de <= 1'b0;
		else if(tx_cnt==data_len-1)
			tx_de <= 1'b0;
		else if(rd_data_count==data_len)
			tx_de <= 1'b1;
		else
			tx_de <= tx_de;
			
	always@(posedge clk)
		if(!rst_n)		
			tx_cnt <= 0;
		else if(tx_cnt==data_len-1)
			tx_cnt <= 0;
		else if(tx_de==1'b1)
			tx_cnt <= tx_cnt + 1'b1;
		else
			tx_cnt <= tx_cnt;
endmodule
```

将这两个控制器与串口收发端一起使用，就实现了带FIFO的串口，如下：
```verilog
uart_tx_buf u1_uart_tx_buf (
	.clk	 		(clk_100M		),	
	.rst_n	 		(rst_n			),
							
	.rx_de			(tx_de			),
    .rx_data		(tx_data		),
						
	.tx_done		(tx_done		),
					
	.tx_de			(uart_tx_de		),
	.tx_data		(uart_tx_data	)	
);

uart_tx #(
    .baud    		(baud ),	
	.check   		(check)		
)	
u1_uart_tx	
(	
	.clk			(clk_100M		),
	.rst_n			(rst_n			),
		
	.tx_de			(uart_tx_de		),
	.tx_data		(uart_tx_data	),
		
	.tx				(tx				),
	.tx_done    	(tx_done		) 
);	

uart_rx #(
    .baud    		(baud  ),
	.check   		(check )
)
u1_uart_rx
(
	.clk			(clk_100M		),
	.rst_n			(rst_n			),
		
	.rx				(rx				),
		
	.rx_de 			(uart_rx_de		),
	.rx_data       	(uart_rx_data	)
);	

uart_rx_buf #(
	.data_len (32) 
)
u1_uart_rx_buf
(
	.clk	 		(clk_100M		),	
	.rst_n	 		(rst_n	 		),
						
	.rx_de			(uart_rx_de		),
    .rx_data		(uart_rx_data	),
						
	.tx_de			(rx_de			),
	.tx_data		(rx_data		)	
);
```

